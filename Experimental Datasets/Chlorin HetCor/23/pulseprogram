;lghetfq (TopSpin 2.1)
;
;HETCOR with FLSG during t1
;B.-J. van Rossum, H. Foerster, and H.J.M. deGroot, J. Magn. Reson. 124 (2003) 516-519
;HF and JOS Oct. 2003
;
;Avance III version
;parameters:
;d1 : recycle delay
;pl1 : for X contact pulse
;sp0 : proton power level during contact
;pl2 : =120dB, not used
;pl12: for decoupling and excitation 1H
;pl13: for homonuclear decoupling
;p3 : 90 degree 1H pulse excitation
;p5 : FSLG 2pi pulse set by lgcalc.incl
;p15 : contact pulse - short 50 - 200 us
;pcpd2 : pulse length in decoupling sequence
;cpdprg2: tppm15, SPINAL64
;cnst20: LG-RF field as adjusted, in Hz used to calculate cnst22 and cnst23 +and - LG frequency
;cnst22: +LG frequency offset 
;cnst23: -LG frequency offset
;cnst24: offset for proton evolution under LG, usually 0 - -2000
;spnam0 : shape for contact pulse ramp.100
;l3 : for dwell in t1 = 4*p5*l3*0.578
;in0 : t1 increment
;ns : 2 or n*4
;FnMODE: undefined
;mc2	: STATES-TPPI
;nd0  : 1
;WDW  : QSINE 
;SSB  : 3 
;zgoptns : -Dlacq, -Dlcp15 or blank
;
;
;$CLASS=Solids
;$DIM=2D
;$TYPE=cross polarisation
;$SUBTYPE=heteronuclear correlation
;$COMMENT=HETCOR (CP based), FLSG during t1

prosol relations=<solids_cp>


define loopcounter count
"count=td1/2"

#include <lgcalc.incl>	
			;calculate p5 and frequency offsets cnst22 and 23
define pulse pul54
"pul54=(p3*547)/900"
define delay dwellf1
"in0=(0.578*l3*4*294/360)/cnst20"
"dwellf1=in0"
"inf1=in0"
"l0=0"
"blktr2=1u"
"blktr1=2u"
;cnst11 : to adjust t=0 for acquisition, if digmod = baseopt
"acqt0=1u*cnst11"

1 ze                             ; zero data
2 d1 do:f2                       ; recycle time, decoupler off
#ifndef lcp15
#include <p15_prot.incl>
			;make sure p15 does not exceed 10 msec 
			;let supervisor change this pulseprogram if 
			;more is needed
#endif
#ifndef lacq		
			;disable protection file for long acquisition change decoupling power !!! or you risk probe damage
			;if you set the label lacq (ZGOPTNS -Dlacq), the protection is disabled

#include <aq_prot.incl>	
			;allows max. 50 msec acquisition time, supervisor
			;may change  to max. 1s at less than 5 % duty cycle
			;and reduced decoupling field
#endif
  1u fq=cnst21:f2                    ; set power level of f2
  dwellf1
  (p3 pl12 ph0):f2
  (pul54 ph1):f2               	 ; magic angle excitation pulse
3 (p5 ph3 fq=cnst22 pl13):f2	;fslg loop at pl13, LG frequ. 1
  (p5 ph4 fq=cnst23):f2		;fslg loop at pl13, LG frequ. 2
  (p5 ph3 fq=cnst22):f2 		;fslg loop at pl13, LG frequ. 1
  (p5 ph4 fq=cnst23):f2		;2 LG periods total
  lo to 3 times l0	
  (pul54 pl12 fq=cnst21 ph7):f2   ;minus magic angle pulse, flip back to xy plane
  (p15 pl1 ph2):f1 (p15:sp0 ph10):f2
				;use short contact times 
				;for direct connectivities only, 50-200 usec
  1u cpds2:f2                   ;start decoupling with tppm at pl12
  go=2  ph31 			;acquire
  1m                  
  10u do:f2                   	;switch off decoupler
  100m wr #0 if #0 zd
  1m ip0
  lo to 2 times 2
4 1m iu0
  lo to 4 times l3
  ;1m rp0 			; for STATES method
  lo to 2 times count
HaltAcqu, 1m		;jump address for protection files
exit                   	;exit
ph0=1 3
ph1=1
ph2=0 0 2 2 1 1 3 3  
ph3=0
ph4=2
ph7=3 
ph10=0
ph31=0 2 2 0 1 3 3 1
    


;$Id: lghetfq,v 1.11 2017/02/22 16:58:55 ber Exp $
